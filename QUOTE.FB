' INSPIRATIONAL QUOTE CODE!
' THE WHOLE THING IS A LOOP.
DO

URL$="N:HTTPS://zenquotes.io/api/random"
NOPEN 8,12,0,URL$
NSTATUS 8

BW = DPEEK($02EA)

JSON$=""
FOR I = 1 TO BW
A=0
NGET 8,&A,1
JSON$=+CHR$(A)
NEXT I

NCLOSE 8

' PARSE THE JSON BECAUSE I CAN'T GET FUJINET TO DO IT
QUOTE$=""
AUTHOR$=""
_QU$="""q"""
_AU$="""a"""
QUSTART=0
QUOTELENGTH=0
AUSTART=0
AULENGTH=0
I=0
COUNTER=1

DO

IF JSON$[COUNTER,3]=_QU$
  ' FIND THE LENGTH OF THE QUOTE
  QUSTART=COUNTER+5
  QUOTELENGTH=0  
  FOR I=COUNTER+5 TO LEN(JSON$)
    IF JSON$[I,1]=CHR$(34)
       COUNTER=I
       EXIT
    ELSE
       INC QUOTELENGTH
    ENDIF
  NEXT I
ENDIF

IF JSON$[COUNTER,3]=_AU$
  ' FIND THE LENGTH OF THE AUTHOR
  AUSTART=COUNTER+5
  AULENGTH=0
  FOR I=COUNTER+5 TO LEN(JSON$)
    IF JSON$[I,1]=CHR$(34)
       EXIT
    ELSE
       INC AULENGTH
    ENDIF
  NEXT I
ENDIF

IF (QUOTELENGTH>1 AND AULENGTH>1)
  EXIT
ENDIF

INC COUNTER
LOOP

QUOTE$=JSON$[QUSTART,QUOTELENGTH]
AUTHOR$=JSON$[AUSTART,AULENGTH]

' PUT THE QUOTE ALL IN UPPERCASE FOR THE RIGHT COLOR
UPQUOTE$=""
FOR I=1 TO LEN(QUOTE$)
IF (ASC(QUOTE$[I,1])>96)
UPQUOTE$=+CHR$(ASC(QUOTE$[I,1])-32)
ELSE
UPQUOTE$=+QUOTE$[I,1]
ENDIF
NEXT I

' PUT THE AUTHOR IN LOWERCASE FOR THE RIGHT COLOR 
DNAUTHOR$=""
FOR I=1 TO LEN(AUTHOR$)
IF (ASC(AUTHOR$[I,1])>64 AND ASC(AUTHOR$[I,1])<91)
DNAUTHOR$=+CHR$(ASC(AUTHOR$[I,1])+32)
ELSE
DNAUTHOR$=+AUTHOR$[I,1]
ENDIF
NEXT I

' PRINT THE QUOTE ON THE SCREEN
GRAPHICS 1
?#6,UPQUOTE$
?#6,DNAUTHOR$

' INPUT MENU FOR WHAT TO DO NEXT
? CHR$(125)
? "(M)ENU, (P)RINT QUOTE, ANY OTHER KEY FOR NEW QUOTE"
GET Q

IF Q=ASC("M")
' RUN THE MENU PROGRAM
XIO #3,39,4,0,"D1:MENU.COM"

ELIF Q=ASC("P")

OPEN #2,8,0,"P:"
?#2,QUOTE$
?#2,AUTHOR$
CLOSE #2

XIO #3,39,4,0,"D1:MENU.COM"

ELIF Q=ASC("X")
EXIT

ENDIF

LOOP


