' PARSE HERSHEY FONT
DIM SEC(96),BYT(96)
DIM FONTLIST$(30),FONTNAME$(30)


' GET THE FONT LIST FROM A FILE
OPEN #1,4,0,"D1:FONTLIST.TXT"
MAXFONT=0
REPEAT
INC MAXFONT
INPUT #1,FONTLIST$(MAXFONT)
INPUT #1,FONTNAME$(MAXFONT)
UNTIL FONTLIST$(MAXFONT)=""
CLOSE #1

@LOADFONT
?CHR$(125)
?"ENTER STRING TO DRAW"
INPUT DSTRING$
?"PRINT SCALE (DEFAULT 1):"
INPUT PSCL
?"COLOR (0-3):"
INPUT PCLR
GRAPHICS 8
COLOR 1
@DRAWSTRING

PROC LOADFONT
GRAPHICS 0
?CHR$(125)
DEC MAXFONT
? "WHICH FONT DO YOU WANT TO USE?" 
FOR I = 2 TO MAXFONT
? I;". ";FONTNAME$(I)
NEXT I
INPUT FCHOICE
' INDEX FILE NAME
IDX$="D1:"
IDX$=+FONTLIST$(FCHOICE)[1,LEN(FONTLIST$(FCHOICE))-3]
IDX$=+"IDX"
' LOAD THE INDEX
COUNTER=1
OPEN #2,4,0,IDX$
COUNTER=0
REPEAT
INC COUNTER
INPUT #2,SEC(COUNTER)
INPUT #2,BYT(COUNTER)
UNTIL ERR()=18
CLOSE #2
ENDPROC

' USING DSTRING$
PROC DRAWSTRING
' NOW WE WILL BE DRAWING AND PRINTING AT THE SAME TIME
OPEN #3,8,0,"P:"
STARTX=20:STARTY=20
PSTARTX=10+(PSCL*10):PSTARTY=-20
?#3,CHR$(27);CHR$(27);CHR$(7)
?#3,"H*I*C";PCLR
FOR COUNTER=1 TO LEN(DSTRING$)

' GO GET THE GLYPH FROM DISK
FONTFILE$="D1:"
FONTFILE$=+FONTLIST$(FCHOICE)
OPEN #1,4,0,FONTFILE$
GNUM = ASC(DSTRING$[COUNTER,1])-31
GLYPH$=""
@POINT SEC(GNUM),BYT(GNUM)
' USING NON INPUT METHOD BECAUSE OF LONG LINE LENGTHS
' INPUT #1,GLYPH$
@GETLINE
CLOSE #1

@DRAWCHAR
NEXT COUNTER
?#3,"M0,";PSTARTY-50;"*I"
CLOSE #3
ENDPROC

PROC GETLINE
DO
GET #1,GL
' EACH GLYPH ENDS WITH A CHR$ 192
IF GL>127 THEN EXIT
GLYPH$=+CHR$(GL)
LOOP
ENDPROC

PROC DRAWCHAR
' EXTRACT GLYPH NUMBER
' THROW AWAY THE FIRST SIX CHARACTERS
' EXTRACT NUMBER OF COMMANDS
NUMCOMMANDS = VAL(GLYPH$[6,3])
' EXTRACT LEFT AND RIGHT LIMITS
LEFTLIMIT = ASC(GLYPH$[9,1]) - ASC("R")
RIGHTLIMIT = ASC(GLYPH$[10,1]) - ASC("R")
' DO WE NEED TO GO TO NEXT LINE?
RLOC=STARTX+ABS(LEFTLIMIT)+ABS(RIGHTLIMIT)
IF RLOC>319
STARTY=STARTY+30
STARTX=20
ENDIF
IF RLOC>479
PSTARTY=PSTARTY-(PSCL*30)
PSTARTX=10+(PSCL*10)
ENDIF


' INITIALIZE POSITION
X = 0
Y = 0

' PARSE COMMANDS
FOR I = 1 TO NUMCOMMANDS-1
CMD$ = GLYPH$[11+(I-1)*2,2]
X = ASC(CMD$[1,1]) - ASC("R")
Y = ASC(CMD$[2,1]) - ASC("R")
' MOVE I AND APOS TO THE LEFT
CURRCH$=CHR$(GNUM+31)
IF (CURRCH$="I") OR (CURRCH$="'")
X = X-5
ENDIF

IF CMD$ = " R"
' PEN LIFT
LIFTPEN=1
ELSE
IF (I=1 OR LIFTPEN=1)
PLOT STARTX+X,STARTY+Y
?#3,"M";PSTARTX+(X*PSCL);",";PSTARTY-(Y*PSCL)
LIFTPEN=0
ELSE
DRAWTO STARTX+X, STARTY+Y
?#3,"D";PSTARTX+(X*PSCL);",";PSTARTY-(Y*PSCL)
ENDIF
ENDIF
NEXT I
STARTX=STARTX+ABS(LEFTLIMIT)+ABS(RIGHTLIMIT)+2
PSTARTX=PSTARTX+((ABS(LEFTLIMIT)+ABS(RIGHTLIMIT)+2)*PSCL)
IF STARTX>319
STARTY=STARTY+30
STARTX=20
ENDIF
IF PSTARTX>479
PSTARTY=PSTARTY-(PSCL*30)
PSTARTX=10+(PSCL*10)
ENDIF
ENDPROC

PROC POINT POINTSEC POINTBYT
  DPOKE $35C, POINTSEC
  POKE $35E,  POINTBYT
  XIO #1,37,PEEK($35A),0,$(0)   ' LAST TWO ARGUMENTS ARE NOT USED
ENDPROC

END

