' DEFINITIONS AREA
DIM WDATA$(8),TEMP%(8),RAIN%(8)
DIM HEADER(240) BYTE,CLOTHING(256)
DIM CLTHIMG(2816) BYTE
GFXTOP=DPEEK(88)

' PROCEDURES AREA
PROC LOADIMGS
   OPEN #1,4,0,"D:ALLCLTH.DAT"
   BGET #1,&CLTHIMG,2816
   CLOSE #1
ENDPROC

PROC NETERROR
?CHR$(125)
?"THERE HAS BEEN A NETWORK ERROR."
?"PLEASE REBOOT THE NETWORK SYSTEM."
GET K
END
ENDPROC

PROC GETWEATHER
' PLEASE REPLACE THIS WITH THE LOCATION OF YOUR WEATHER.TXT
   URL$="N:http://segan-atari.local:8000/weather.txt"
   NOPEN 2,12,0,URL$
   NSTATUS 2
IF PEEK($02ED)>1
@NETERROR
ENDIF   
' GET THE DATA STRING
BYTS=DPEEK($02EA)
   INDEX=1
   NETBYTE=0
   FOR I=1 TO BYTS
       NGET 2,&NETBYTE,1
       IF NETBYTE=10
          INC INDEX
       ELSE
          WDATA$(INDEX)=+CHR$(NETBYTE)
       ENDIF
   NEXT I
   NCLOSE 2
   FOR I=2 TO 8
   ' THROW OUT HEADER LINE
     TEMP%(I-1)=VAL(WDATA$(I)[22,4])
     RAIN%(I-1)=VAL(WDATA$(I)[27,3])
   NEXT I
ENDPROC

PROC DRAWCLOTHES L C
' L IS LOCATION, 1 TO 4
' C IS CLOTHING ITEM
' 1=umbrella 2=heavy coat 3=midweight coat
' 4=sweater 5= long sleeves 6= short sleeves
' 7=winter hat 8=summer hat 9=gloves
' 10=pants 11=shorts
CSTART = ((C-1)*(8*32))
IF L=1
   XSTART=5
   YSTART=10
ELIF L=2
   XSTART=20
   YSTART=10
ELIF L=3
   XSTART=5
   YSTART=44
ELSE
   XSTART=20
   YSTART=44
ENDIF
' MOVE THE IMAGE DATA DIRECTLY INTO THE RIGHT SPOT ON THE SCREEN
IMGSRC=&CLTHIMG+CSTART
IMGDEST=DPEEK(88)+XSTART+(YSTART*40)
CINDEX=CSTART
FOR I=1 TO 32
   MOVE IMGSRC,IMGDEST,8
   IMGSRC=IMGSRC+8
   IMGDEST=IMGDEST+40
NEXT I
ENDPROC


PROC CHECKRAIN
   RAINTEXT$="NO RAIN TODAY."
   IF RAIN%(1)>0
   RAINTEXT$="IT'S RAINING RIGHT NOW!"
   @DRAWCLOTHES 4,1
   ELSE
   FOR R=2 TO 7
     IF RAIN%(R)>0
     RAINTEXT$="IT WILL RAIN IN "
     RAINTEXT$=+STR$(RAIN%(R))
     RAINTEXT$=+" HOURS!"
     @DRAWCLOTHES 4,1
     EXIT
     ENDIF
   NEXT R
   ENDIF
ENDPROC

PROC GETCLOTHES
NOW%=TEMP%(1)
MIN%=TEMP%(1)
MAX%=TEMP%(1)
FOR I = 1 TO 7
    IF TEMP%(I)<MIN% THEN MIN%=TEMP%(I)
    IF TEMP%(I)>MAX% THEN MAX%=TEMP%(I)
NEXT I
' 1=umbrella 2=heavy coat 3=midweight coat
' 4=sweater 5= long sleeves 6= short sleeves
' 7=winter hat 8=summer hat 9=gloves
' 10=pants 11=shorts

DEGREES$="IT IS CURRENTLY "
DEGREES$=+STR$(NOW%)
DEGREES$=+" DEGREES. IT WILL GO UP TO "
DEGREES$=+STR$(MAX%)
DEGREES$=+" AND DOWN TO "
DEGREES$=+STR$(MIN%)

IF NOW%<20
   CLOTHES$="HEAVY COAT, HAT, GLOVES"
   @DRAWCLOTHES 1,2
   @DRAWCLOTHES 2,7
   @DRAWCLOTHES 3,9
ELIF NOW%<35
   CLOTHES$="HEAVY COAT, HAT"
   @DRAWCLOTHES 1,2
   @DRAWCLOTHES 2,7
   @DRAWCLOTHES 3,10
ELIF NOW%<40
   CLOTHES$="JUST A HEAVY COAT"
   @DRAWCLOTHES 1,2
   @DRAWCLOTHES 2,10
ELIF NOW%<50
   CLOTHES$="MIDWEIGHT COAT, BRING A HAT"
   @DRAWCLOTHES 1,3
   @DRAWCLOTHES 2,7
   @DRAWCLOTHES 3,10
ELIF NOW%<55
   CLOTHES$="JACKET OVER SWEATER"   
   @DRAWCLOTHES 1,4
   @DRAWCLOTHES 2,10
ELIF NOW%<60
   CLOTHES$="SWEATER, JEANS"
   @DRAWCLOTHES 1,4
   @DRAWCLOTHES 2,10
ELIF NOW%<70
   CLOTHES$="LONG SLEEVES, JEANS"
   @DRAWCLOTHES 1,5
   @DRAWCLOTHES 2,10
ELIF NOW%<80
   CLOTHES$="SHORT SLEEVES, JEANS"
   @DRAWCLOTHES 1,6
   @DRAWCLOTHES 2,10
ELSE
   CLOTHES$="SHORT SLEEVES, SHORTS, SUMMER HAT"
  
 @DRAWCLOTHES 1,6
   @DRAWCLOTHES 2,11
   @DRAWCLOTHES 3,8  
ENDIF
ENDPROC 

PROC DRAWHEADER
   OPEN #1,4,0,"D1:HEADER.DAT"
   FOR I = 1 TO 320
   GET #1,HVAL
   POKE GFXTOP+I,HVAL
   NEXT I
   CLOSE #1
ENDPROC


' PROGRAM AREA
GRAPHICS 7
COLOR 1
GFXTOP=DPEEK(88)
@DRAWHEADER
@LOADIMGS
@GETWEATHER

@CHECKRAIN
@GETCLOTHES

POKE 752,1
?DEGREES$;
?". SO WEAR ";
?CLOTHES$;
?". ";
?RAINTEXT$;

GET K

END
